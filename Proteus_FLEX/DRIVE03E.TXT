* DRIVE03D  14/7/82
 STTL DRIVE03D MODIFIED DRIVER02
 OPT PAG
 PAG
*
* FOR SLIM-LINE DRIVES
****** THIS FILE MUST HAVE R OPTION SPECIFIED ELSE
* LBSR TIMING LOOP WILL BE OPTIMISED TO BSR  ON ASM09
*
*        EQUATES FOR DISK CONTROLLER TYPE 1771
*
SEEK EQU $18 SEEK COMMAND.
*                         LOAD HEAD, 6MS STEP RATE
RESTOR EQU $08 RESTORE COMMAND.
*                         LOAD HEAD, 6MS STEP RATE
SECTRS EQU $10 NUMBER OF SECTORS

DRVREG EQU $E014
COMREG EQU $E018
TRKREG EQU $E019
SECREG EQU $E01A
DATREG EQU $E01B

 ORG $DE00

DREAD JMP DDRSEC

DWRITE JMP DDWSEC

DVERFY JMP VERIFY

DRESTR JMP TRCK00

DSELCT JMP DRSEL

DCKRDY JMP CDRRDY

DQCRDY JMP QDRRDY

DDCOLD JMP COLDST

DDWARM JMP WARMST

DDSEEK JMP TSSEEK

CURDRV FCB 0 CURRENT DRIVE NUMBER

LTRKNO FCB 0 TRACK TEMP
 FCB 0 TRK LAST ACCESSED ON DRIVE 0
 FCB 0 TRK LAST ACCESSED ON DRIVE 1
 FCB 0 TRK LAST ACCESSED ON DRIVE 2
 FCB 0 TRK LAST ACCESSED ON DRIVE 3

*
* DREAD - A=TRK B=SEC X=FCB
DDRSEC BSR TSSEEK READ SECTOR
 SEI DISABLE IRQ
 LDA #$88 READ, LOAD HEAD AT BEGIN
 STA COMREG ISSUE COMMAND
 BSR DDLY1 DELAY
 CLRB .SET COUNT TO 256
*                         BYTES/SECTOR MAXIMUM
 PSHS DP SAVE DP REGISTER
 LDA #$E0 SET DIRECT PAGE TO
*                         $E000 TEMPORARILY
 TFR A,DP
 SETDP $E0
 BRA DDRL2 GO AND CHECK TO SEE
*                         IF DATA AVAILABLE

DDRL1 LDA <DATREG READ BYTE
 STA 0,X+ STORE IN BUFFER
DDRL2 LDB <COMREG READ STATUS REGISTER
 BITB #2 READY FOR NEXT BYTE?
 BNE DDRL1 YES BRANCH
 BITB #1 READ SECTOR COMPLETED
 BNE DDRL2 NOT COMPLETE LOOK FOR
*                         ANOTHER BYTE
 PULS DP RESTORE ORIGINAL DP REGISTER
 SETDP $0
 CLI ENABLE IRQ
RDDONE BITB #$9C NOT READY OR RECORD
*                         NOT FOUND OR CRC ERROR
*.                           OR LOST DATA
 RTS

WTDONE LDB COMREG READ DISK STATUS REGISTER
 BITB #$01 STILL BUSY
 BNE WTDONE YES LOOP TILL NOT BUSY
 RTS

*
* DDSEEK - SEEK TO TRACK/SECTOR
TSSEEK STB SECREG STORE SECTOR NUMBER
*                         INTO SECTOR REGISTER
 CMPB #SECTRS SECTORS >=11 ARE
*                         ON SIDE 2 OF DISK
 LDB #0 PREPARE FOR SIDE 1
 BCS SIDE1 IS SIDE 1
 LDB #$40 PREPARE FOR SIDE 2
SIDE1 ORB CURDRV OR SIDE WITH DRIVE NUMBER
 STB DRVREG STORE AT DRIVE
*                         SELECT REGISTER
 CMPA TRKREG ALREADY ON RIGHT TRACK?
 BEQ CHKRDY YES- CHECK READY AND RETURN
 BLO STEPOUT TO TRK 0 DIRECTION
 LDB #$59 STEP IN WITH 06 MSEC STEPPING RATE
 BRA STEPCM
STEPOUT LDB #$79 STEP OUT WITH 06 MSEC STEPPING RATE
STEPCM STB COMREG
 BSR DDLY1
 BSR WTDONE WAIT TILL DONE
 STA DATREG NO - SET TRACK SEEK REGISTER
 BSR CHKRDY ENSURE UP TO SPEED
 LDA #SEEK SEEK HEAD LOADED
*                         AT START 6MS STEP RATE
 STA COMREG TO COMMAND REGISTER
 BSR DDLY1 DELAY
 PSHS B,X
 BSR WTDONE WAIT TILL DONE, AND RETURN
 LDX #$0140 ALLOW 15MSEC SETTLING TIME
WAITB BSR DDLY1
 LEAX -1,X
 BNE WAITB
 PULS X,B,PC
*IF NOT READY (TURNING) WAIT 0.5 SEC TO ENSURE UP TO SPEED
CHKRDY PSHS B
 LDB COMREG
 BPL RDY
 BSR WAITSP
RDY PULS B,PC

WAITSP PSHS X
 LDX #15000
W1LOOP BSR DDLY1
 LEAX -1,X
 BNE W1LOOP
 PULS X,PC

DDLY1 LBSR DDLY2 **** MUST BE LBSR
DDLY2 LBSR DDLY3 **** MUST BE LBSR
DDLY3 RTS

*
*  DWRITE - A=TRK B=SEC X=FCB
DDWSEC BSR TSSEEK SEEK TRACK
 SEI DISABLE IRQ
 LDA #$A8 WRITE, LOAD HEAD AT BEGIN
 STA COMREG TO COMMAND REGISTER
 BSR DDLY1 DELAY
 PSHS DP SAVE DP REGISTER
 LDA #$E0 SET DIRECT PAGE TO
*                         $E000 TEMPORARILY
 TFR A,DP
 SETDP $E0
 BRA DDWL2 GO AND PRELOAD A
*                         WITH FIRST BYTE OF
*.                           BUFFER
DDWL1 STA <DATREG STORE TO DISK
DDWL2 LDA 0,X+ GET BYTE FROM BUFFER
DDWL3 LDB <COMREG READ STATUS REGISTER
 BITB #2 READY FOR NEXT BYTE?
 BNE DDWL1 YES BRANCH
 BITB #1 WRITE SECTOR COMPLETED?
 BNE DDWL3 NOT COMPLETED LOOP
 PULS DP RESTORE ORIGINAL DP REGISTER
 SETDP $0
 CLI ENABLE IRQ
 BITB #$DC NOT READY OR WRITE
*                         PROTECT OR RECORD NOT
*.                           FOUND OR CRC ERROR
*                             OR LOST DATA
 RTS

*
* DVERFY - VERIFY  (NO PARAMETETRS)
VERIFY LDA #$88 READ, LOAD HEAD AT BEGIN
 STA COMREG
 BSR DDLY1 DELAY
 LBSR WTDONE WAIT TILL READ FINISHED
*                         & CHECK STATUS REG
 BITB #$98 DRIVE NOT READY OR
*                         RECORD NOT FOUND OR
*.                           CRC ERROR
 RTS

*
*  DRESTR - RESTORE TO TRACK 00
TRCK00 PSHS X
 BSR DRSEL SELECT DRIVE
 LDA #RESTOR RESTORE
 STA COMREG ISSUE COMMAND
 BSR DDLY1 DELAY
 LBSR WTDONE WAIT TILL READY AND
*                         RETURN STATUS REG IN B
 PULS X
 BITB #$40 WRITE PROTECT DISK?
 BNE WRPROT YES
 CLC NO - NO ERROR
 RTS

WRPROT LDB #$16 "DISK WRITE PROTECTED"
 ASRB CLEAR CARRY
 RTS

*
*  DSELCT - DRIVE SELECT
DRSEL LDA $03,X GET DRIVE NUMBER
 CMPA #4 MUST BE < 4
 BLO DRVOK
 LDB #$1F "ILLEGAL DRIVE NUMBER"
 ASRB
 RTS

DRVOK BSR LSTTRK POINT X TO LAST TRACK
*                         NO. ACCESSED OF
*.                           CURRENT DRIVE
 LDB TRKREG READ CURRENT TRACK NUMBER
 STB ,X STORE AS LAST ACCESSED TRACK
*                         ON DRIVE "N"
 STA DRVREG STORE DRIVE NO.
*                         IN DRIVE SELECT REGISTER
 CMPA CURDRV SAME AS LAST DRIVE ACCESSED?
 BEQ SAMDRV YES- NO NEED TO WAIT
*                         WHILE DRIVE SELECTED
 BSR DDLY1 DELAY WHILE DRIVES
*                         ARE RE-SELECTED
SAMDRV STA CURDRV STORE THIS A
*                         "LAST" DRIVE ACCESSED
 BSR LSTTRK POINT X TO LAST TRACK
*                         NUMBER ACCESSED
*.                           OF CURRENT DRIVE
 LDA ,X GET LAST TRACK ACCESSED
 STA TRKREG PUT INTO TRACK REGISTER
 BRA CRDY2 CHECK DRIVE READY

LSTTRK LDX #LTRKNO
 LDB CURDRV POINT X TO NO. OF
*                         TRACK LAST ACCESSED ON
*.                           DRIVE
 ABX
 RTS

*
*  DQCRDY - DRIVE QUICK CHECK READY
QDRRDY LDA 3,X SELECT DRIVE AS REQUIRED
 STA DRVREG
CRDY3 LBSR DDLY1  DELAY IN CASE NEW DRIVE
 CLC CLEAR ERROR BIT
 LDB COMREG READY?
 BPL RDYEND

NREDY SEC NOT READY
RDYEND RTS

*

*  DCKRDY - DRIVE CHECK READY
*
CDRRDY LDA 3,X
 STA DRVREG SELECT DRIVE
CRDY2 BSR CRDY3 CHECK IF READY
 BCC RDYEND EXIT IMMEDIATELY IF READY
 LBSR WAITSP ELSE WAIT TILL SPED UP
 BRA CRDY3 THEN TEST AGAIN
*  DDCOLD - COLD START ENTRY
COLDST LDX #CURDRV
 LDB #6 CLEAR SIX BYTES
DDCLP CLR ,X+ INCLUDING LAST TRACKS
*                         ACCESSED ON
 DECB DRIVES 0-4
 BNE DDCLP LOOP TILL DONE
*
*  DDWARM - WARM START ENTRY
WARMST RTS


* F283MODF
* Control R code added (called from CE2D Inbuff)
*
* DOS TITLES.  POLYCORP MODS/ADDITIONS
*
 ORG $C810

FLEXTL FCC "POLYFLEX - Version"
 FCC "  2.8:3 - 1.1:2 - "

RAMSIZ FCC "32K "

 IF *<>$C838
 ERR LABEL LC838 IS NOT AT $C838
 ENDIF

LC838 FCC "RAM",EOT

 ORG $C840

SYSFCB FCB $FF,$00,$00,$00

LC844 FCC "startup",NUL,"txt",NUL

START ORCC #$50 DISABLE FIRQ AND IRQ
 LDS #LINBUF
 LDA #1
 STA PT6840+1 RESET 6840 TIMER
 STA PT6840

* TEST FOR 32K OR 56K
 LDX #$5FFF TOP OF 32K USER
 LDA #$A5 TEST PATTERN
 CLRB CLEAR FLAG
 LDY 1,X SAVE DATA
 STD 1,X SEE IF MEM PRESENT @ $6000
 CMPD 1,X DID IT WRITE?
 BNE START2 NO - 32K
 STY 1,X PUT BACK DATA
 LDD #'5<<8+'6
 LDX #$BFFF YES - 56K
START2
 STX MEMEND
 TSTB
 BEQ NOCHNG
 STD RAMSIZ CHANGE FLEX MESSAGE

NOCHNG LDA #SPORTS NUMBER OF SERIAL PORTS
 STA CPUTYP
 JSR [INITSBUG] INITIALISE SBUG
 JMP COLDS

* MODIFIED DATE PROMPT MOVED FROM $C82E
DATMES FCC "Date (DD,M"

 FCC "M,YY)? ",EOT

 ORG $CB00

* ADDITIONAL CODE TO REPORT COMMAND NOT FOUND
* ON 'SYSTEM DRIVE'
* PROGRAM JUMPS HERE FROM $D331
CMNOTF JSR Rpterr
 JMP LCDDE
*
* EXTRA CODE FOR REPORT ERROR
*
CNTFND
 CMPA #4
 BNE CNTFED
 LDX #NOTFND
 JSR Pstrng
 LDX 2,S    POINT TO FCB
 LDA DRVENO,X GET DRIVE NO
 BMI AUTDRV DON'T PRINT DRV NO
 ORA #$30 CONVERT TO ASCII
 JSR Putchr
 LDA #'.
 JSR Putchr
AUTDRV LEAX 36,X
 LDB #8
 BSR OUTNAM
 LDA #'.
 JSR Putchr
 LDB #3
 BSR OUTNAM
 LDA #'"
 JSR Putchr
 ORCC #$04 SET EQUAL CONDITION
CNTFED RTS
*
OUTNAM
 LDA 0,X+
 BEQ SKPCHR
 JSR Putchr
SKPCHR
 DECB
 BNE OUTNAM
 RTS

* MODIFIED NOT FOUND PROMPT MOVED FROM $CC65
NOTFND FCC 'Cannot find "',EOT

* Additional code to process control R on DOS
* command line. Reprints current line buffer
*
NEWINB STX LINPTR
 JSR Getchr
 CMPA #$12 control R ?
 LBNE NOTREP no return to Inbuff
 PSHS X save LINBUF

LOOKCR LDA ,X+ step thru line buff
 CMPX #CMDADR
 BHS ENDCML END OF CMD LINE
 CMPA #$0D  till CR
 BNE LOOKCR
ENDCML
 LDA #4 set EOL
 STA ,-X replace CR with EOT
 PULS X recover LINBUF
 JSR PSTRG1
 JMP LCE30 return to Inbuff 


WOPEN    LDX   FCBCADR       LOAD X WITH CURRENT FCB ADDRESS
         TST   DRIVENO,X     TEST DRIVE NUMBER
         BPL   NOTAUT        BRANCH IF NO AUTOMATIC DRIVE
         JSR   FNDRIVE       SEARCH FOR NEXT READY DRIVE
         BCC   NOTAUT        BRANCH IF DRIVE O K
         LDB   #$10          DRIVES NOT READY
         RTS                  .EXIT

NOTAUT   JSR   CLRFCB        CLEAR FCB AREA
         JSR   LD917
         BCS   SIRERR        READ SYSTEM INFO ERROR
         JSR   FSTAT         CHECK FILE STATUS
         BCS   REMFCBC       BRANCH IF ERROR TO REMOVE FCB FROM OPEN CHAIN
         JSR   FDIRET        FIND MATCHING DIR ENTRY
         BCS   REMFCBC       BRANCH IF ERROR TO REMOVE FCB FROM OPEN CHAIN
         BNE   NOTHER        BRANCH IF FILE DOES NOT EXIST
         LDB   #$03          THE FILE SPECIFIED ALREADY EXISTS
         BRA   REMFCBC       BRANCH IF ERROR TO REMOVE FCB FROM OPEN CHAIN

NOTHER   JSR   LDCC1
         BCS   REMFCBC       BRANCH IF ERROR TO REMOVE FCB FROM OPEN CHAIN
         LDX   FCBCADR
         LDB   #$0A          LOAD B WITH COUNT OF 10
LDA0A    CLR   $0F,X
         LEAX  $01,X         INCREMENT X BY ONE
         DECB                .DECREMENT B BY 1
         BNE   LDA0A
         LDX   FCBCADR
         LDD   FSTDIR,X      IS THERE ANY DELETED ENTRY?
         BEQ   NODELT        BRANCH IF NO DELETED ENTRY
         STD   CDIRAD,X      SET DIR POSN TO 1ST DELETED ENTRY
         LDA   FSTDIR+2,X    FIRST DELETED ENTRY INDEX
         STA   CDIRAD+2,X    SET AS CURRENT DIR INDEX
         LDD   SYSMONTH      SET MONTH
         STD   $19,X
         LDA   SYSYEAR
         STA   $1B,X
         JSR   LDDAE         MOVE FN/EXT FROM FCB+$24 TO FCB+4
         JSR   ZZZZC
         BCS   REMFCBC       BRANCH IF ERROR TO REMOVE FCB FROM OPEN CHAIN
         BSR   SETACT         SET ACTIVITY STATUS
         LDA   #$04
         STA   DATINX,X      SET DATA INDEX TO BEGINNING OF BUFFER
         CLC
SIRERR   RTS                  .EXIT

. NO DELETED DIR ENTRY -EXTEND DIR SECTOR
NODELT   LDX   FCBCADR
         CLR   FLSMAP,X      CLEAR SECTOR MAP FLAG
         INC   FLSTRT+1,X
         LDD   CDIRAD,X      LOAD T/S ADDRESS
         JSR   RDSECT        READ A SECTOR
         BCS   REFCB         BRANCH IF ERROR
         JSR   NOTFST        WRITE SECTOR & RESERVED NEXT
         BCS   REFCB         BRANCH IF ERROR
         JSR   WSSECT        WRITE A SECTOR
         BCC   RESDIR        RESERVE FIRST DIR ENTRY OF THE SECTOR
         JSR   LDBC1
REFCB    JMP   REMFCBC       BRANCH IF ERROR TO REMOVE FCB FROM OPEN CHAIN

. NOW GET FIRST DIR ENTRY OF NEW DIR SECTOR
RESDIR   LDX   FCBCADR
         LDD   CURPOS,X      CURRENT T/S
         STD   FSTDIR,X      FIRST DELETED ENTRY
         LDA   #$10
         STA   FSTDIR+2,X    SET DIR INDEX TO FIRST ENTRY
         JSR   RWSIR         RE WRITE SYSTEM INFO REC
         BCS   REFCB
         BRA   NOTHER        NOW WRITE DIR ENTRY

. SET ACTIVITY STATUS,CLEAR COMPRESSION MODE
SETACT   LDX   FCBCADR
         LDA   FNCODE,X       LOAD FUNCTION CODE
         STA   ACTSTAT,X      SET ACTIVITY STATUS
         CLR   FNCODE,X
         CLR   SPCOMFLG,X     CLEAR SPACE COM MODE
         CLRA
         STA   DATINX,X       DATA INDEX
         RTS                  .EXIT

